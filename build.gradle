plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.3'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id "com.github.ben-manes.versions" version "0.20.0"
}

group 'com.github.samophis'
description 'Fast client implementation for Lavalink.'
version '2.4.4'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Credit to natanbc for this -- I'm shit at Gradle

task updateDocs(type: Copy) {
    dependsOn javadoc
    from "${buildDir}/docs/javadoc"
    into "docs"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
}

shadowJar {
    classifier = 'withDependencies'
}

// Credit to the JDA Developers

bintray {
    user = bintrayUsername
    key = bintrayApiKey
    publications = ['BintrayRelease']
    pkg {
        repo = 'maven'
        name = 'LavaClient'
        licenses = ['Apache-2.0']
        vcsUrl = ['https://github.com/SamOphis/LavaClient.git']
        publish = true
        version {
            name = project.version
            released = new Date()
        }
    }
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId group
            artifactId 'LavaClient'
            version version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

build {
    dependsOn clean
    dependsOn jar
    dependsOn javadocJar
    dependsOn sourcesJar
    dependsOn shadowJar
    dependsOn updateDocs
    dependsOn test

    jar.mustRunAfter clean
    javadocJar.mustRunAfter jar
    sourcesJar.mustRunAfter javadocJar
    shadowJar.mustRunAfter sourcesJar
}

// Credit to the JDA Development Team

String getProjectProperty(String propertyName)
{
    String property = ""
    if (hasProperty(propertyName))
    {
        property = this.properties[propertyName]
    }
    return property
}

bintrayUpload {
    dependsOn build

    onlyIf { !getProjectProperty("bintrayUsername").empty }
    onlyIf { !getProjectProperty("bintrayApiKey").empty }
}

javadoc {
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.author()
    options.encoding = 'UTF-8'

    exclude('samophis/lavalink/client/entities/builders')
    exclude('samophis/lavalink/client/entities/events')
    exclude('samophis/lavalink/client/entities/internal')
    exclude('samophis/lavalink/client/entities/messages')
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

repositories {
    jcenter()
}

ext {
    wsVersion = '2.5'
    jacksonVersion = '2.9.6'
    jsoniterVersion = '0.9.22'
    javassistVersion = '3.12.1.GA'
    httpAsyncClientVersion = '4.1.3'
    fastutilVersion = '8.2.1'
    sentryLogbackVersion = '1.7.5'
    findbugsSourcesVersion = '3.0.2'
    lavaplayerSourcesVersion = '1.3.6'
    caffeineVersion = '2.6.2'
}

dependencies {
    compile group: 'com.neovisionaries', name: 'nv-websocket-client', version: wsVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: jacksonVersion
    compile group: 'com.jsoniter', name: 'jsoniter', version: jsoniterVersion
    compile group: 'javassist', name: 'javassist', version: javassistVersion
    compile group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: httpAsyncClientVersion
    compile group: 'it.unimi.dsi', name: 'fastutil', version: fastutilVersion
    compile group: 'io.sentry', name: 'sentry-logback', version: sentryLogbackVersion
    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: findbugsSourcesVersion
    compileOnly group: 'com.sedmelluq', name: 'lavaplayer', version: lavaplayerSourcesVersion
    compile group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: caffeineVersion
}

sourceCompatibility = 1.8
targetCompatibility = 1.8